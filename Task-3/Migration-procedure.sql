CREATE OR REPLACE PROCEDURE Migrate_Data_From_XXBCM_ORDER_MGT AS
BEGIN
    -- Insert data into Suppliers table
    INSERT INTO SUPPLIER (NAME, CONTACT_NAME, ADDRESS, CONTACT_NUMBER, EMAIL)
    SELECT DISTINCT SUPPLIER_NAME, SUPP_CONTACT_NAME, SUPP_ADDRESS, SUPP_CONTACT_NUMBER, SUPP_EMAIL
    FROM XXBCM_ORDER_MGT;

    -- Insert data into Orders table
    INSERT INTO ORDERS (REFERENCE, ORDER_DATE, SUPPLIER_ID, TOTAL_AMOUNT, DESCRIPTION, STATUS)
    SELECT DISTINCT ORDER_REF,           
    	CASE
	    	WHEN REGEXP_LIKE(ORDER_DATE, '^\d{2}-\w{3}-\d{4}$') THEN TO_DATE(ORDER_DATE, 'DD-MON-YYYY')
	    	ELSE TO_DATE(ORDER_DATE, 'DD-MM-YYYY')
	    END, 
	    S.ID, 
	    CASE 
        	WHEN REGEXP_LIKE(ORDER_TOTAL_AMOUNT, '^\d+(\.\d+)?$') THEN TO_NUMBER(ORDER_TOTAL_AMOUNT)
            ELSE NULL
        END, 
	    ORDER_DESCRIPTION, ORDER_STATUS
    FROM XXBCM_ORDER_MGT O
    INNER JOIN SUPPLIER S ON O.SUPPLIER_NAME = S.Name;

    -- Insert data into Order_Lines table
    INSERT INTO ORDER_LINES (ORDER_ID, LINE_AMOUNT)
    SELECT O.ID, 
    	CASE 
        	WHEN REGEXP_LIKE(ORDER_LINE_AMOUNT, '^\d+(\.\d+)?$') THEN TO_NUMBER(ORDER_LINE_AMOUNT)
            ELSE NULL
        END
    FROM XXBCM_ORDER_MGT M
    INNER JOIN ORDERS O ON M.ORDER_REF = O.REFERENCE;

    -- Insert data into Invoices table
    INSERT INTO INVOICE (REFERENCE, INVOICE_DATE, ORDER_ID, AMOUNT, DESCRIPTION, STATUS, HOLD_REASON)
    SELECT DISTINCT INVOICE_REFERENCE, 
    	CASE
        	WHEN REGEXP_LIKE(INVOICE_DATE, '^\d{2}-\w{3}-\d{4}$') THEN TO_DATE(INVOICE_DATE, 'DD-MON-YYYY')
    		ELSE TO_DATE(INVOICE_DATE, 'DD-MM-YYYY')
		END,
    	O.ID, 
    	CASE 
        	WHEN REGEXP_LIKE(INVOICE_AMOUNT, '^\d+(\.\d+)?$') THEN TO_NUMBER(INVOICE_AMOUNT)
        	ELSE NULL 
        END,
    	INVOICE_DESCRIPTION, INVOICE_STATUS, INVOICE_HOLD_REASON
    FROM XXBCM_ORDER_MGT M
    INNER JOIN ORDERS O ON M.ORDER_REF = O.REFERENCE;
    
    COMMIT;
    
    DBMS_OUTPUT.PUT_LINE('Data migration from XXBCM_ORDER_MGT completed successfully.');
EXCEPTION
    WHEN OTHERS THEN
        ROLLBACK;
        DBMS_OUTPUT.PUT_LINE('Error during data migration: ' || SQLERRM);
END;